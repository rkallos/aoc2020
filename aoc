#!/usr/bin/env tclsh

::tcl::tm::path add ./

set usageMessage ""
append usageMessage \n {Usage:}
append usageMessage \n "[info script] -day day -part part -input inputFileName"
append usageMessage \n {}

set baseDir [file dirname [info script]]

array set now [clock format [clock seconds] -format {year %Y month %m day %d}]
set Selected(year) $now(year)
set Selected(day) [expr {($now(month) == 12) ? $now(day) : 1}]
set Selected(part) 0
set Selected(input) {}
set Selected(test) 0

foreach {name value} $argv {
    switch -exact -- $name {
        -year {
            set Selected(year) $value
        }
        -day {
            set Selected(day) $value
        }
        -part {
            set Selected(part) $value
        }
        -input {
            set Selected(input) $value
        }
        -test {
            set Selected(test) $value
        }
    }
}

# e.g. prologue. https://wiki.tcl-lang.org/page/e%2Eg%2E?R=0
if {$Selected(test)} {
    # run inline tests
    set Ntest 0; set Nfail 0
    proc e.g. {cmd -> expected} {
        incr ::Ntest
        catch {uplevel 1 $cmd} res
        if {$res ne $expected} {
            puts "$cmd -> $res, expected $expected"
            incr ::Nfail
        }
    }
} else {
    # don't run inline tests
    proc e.g. args {}
}

set nsName [format {day%2.2d} $Selected(day)]
set fName [format {%4.4d/%s} $Selected(year) $nsName.tcl]
set fPath [file join $baseDir $fName]
if {![file exists $fPath]} {
    puts stderr "$fPath is missing"
    exit 1
} else {
    source $fPath
}

# e.g. epilogue. Show stats
if {$Selected(test)} {
    set egname [format {%4.4d-day%2.2d} $Selected(year) $Selected(day)]
    puts "$egname : tested $::Ntest, failed $::Nfail"
    exit 0
}

set iPath [format {%s/%4.4d/input/day%2.2d} $baseDir $Selected(year) $Selected(day)]
if {$Selected(input) ne {}} {
    set iPath $Selected(input)
    set usingFile 1
} elseif {[file exists $iPath]} {
    file join $baseDir $Selected(year) $Selected(day) input
    set usingFile 1
} else {
    set usingFile 0
}

if {$usingFile} {
    if {[catch {open $iPath r} fid]} {
        puts stderr "Unable to read $iPath: $errorInfo"
        exit 1
    }
    set Selected(data) [read $fid]
} else {
    puts {reading input from stdin}
    set Selected(data) [read stdin]
}

proc runPart {part} {
    global nsName
    global Selected
    set func [join [list $nsName "::" part$part] ""]
    puts "day $Selected(day) part $part: [$func $Selected(data)]"
}

if {$Selected(part) eq 1 || $Selected(part) eq 2} {
    runPart $Selected(part)
} else {
    runPart 1
    runPart 2
}
